plugins {
    id 'java'
    id 'java-library'
}

group = 'six.eared.macaque'

repositories {
    mavenCentral()
}

ext {
    projectVersion = '1.0.0-SNAPSHOT'
    logbackVersion = '1.2.3'
    fastjsonVersion = '2.0.6'
    projectUrl = 'https://github.com/haiyanghan/macaque-hotswap'
    mavenUsername = System.getenv('MVN_USER_NAME') != null ? System.getenv('MVN_USER_NAME') : ''
    mavenPassword = System.getenv('MVN_PASSWORD') != null ? System.getenv('MVN_PASSWORD') : ''
}


allprojects {
    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        [compileJava]*.options*.encoding = 'UTF-8'
    }
    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        [compileTestJava]*.options*.encoding = 'UTF-8'
    }
}

subprojects {
    def publish = ['macaque-mbean', 'macaque-common'].contains(project.name)
    if (publish) {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        publishing {
            repositories {
                maven {
                    name 'releaseRepository'
                    url 'https://s01.oss.sonatype.org/content/repositories/releases/'
                    credentials {
                        username = "${mavenUsername}"
                        password = "${mavenPassword}"
                    }
                }
                maven {
                    name = 'snapshotRepository'
                    url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    credentials {
                        username = "${mavenUsername}"
                        password = "${mavenPassword}"
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    afterEvaluate {
                        ["srcJar", "javadocJar"].forEach { taskName ->
                            def task = tasks.findByName(taskName)
                            if (task != null)
                                artifact task
                        }
                        from components.java
                        groupId = 'io.github.haiyanghan'
                        version = projectVersion
                        artifactId = 'six-eared-' + archivesBaseName
                        pom {
                            name = project.name
                            url = "${projectUrl}"
                            licenses {
                                license {
                                    name = 'The Apache License, Version 2.0'
                                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                }
                            }

                            scm {
                                connection = "scm:git:${projectUrl}.git"
                                developerConnection = "scm:git:${projectUrl}.git"
                                url = "${projectUrl}"
                            }
                        }
                    }
                }
            }
        }
    }
}