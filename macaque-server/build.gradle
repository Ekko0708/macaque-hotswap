plugins {
    id 'java'
    id 'application'
}

group = 'six.eared.macaque'

def projectName = 'macaque-server'

repositories {
    mavenCentral()
}

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    def javaHome = System.getenv 'JAVA_HOME'

    implementation files("${javaHome}/lib/tools.jar")
    implementation project(":macaque-common")
    testImplementation 'junit:junit:4.13.1'
}

jar {
    archiveBaseName = "${projectName}"
    archiveVersion = "${projectVersion}"
    manifest {
        attributes(
                'Main-Class': 'six.eared.macaque.server.MacaqueBootStrap',
                'Manifest-Version': "${projectVersion}",
                'Class-Path': configurations.runtimeClasspath.files.collect {
                    it.getName().contains("tools.jar") ? "" : "../libs/" + it.getName()
                }.join(' ')
        )
    }
}

task release(type: Tar, group: build) {
    from(jar) {
        into('bin/')
    }
    from(project(":macaque-agent").shadowJar) {
        into('bin/')
    }
    from("${rootDir}/bin") {
        into('bin/')
    }
    from(configurations.runtimeClasspath.files) {
        into('libs/')
    }

    compression = Compression.GZIP
    archiveBaseName = "${projectName}"
    archiveVersion = "${projectVersion}"
    archiveExtension = 'tar.gz'
}

